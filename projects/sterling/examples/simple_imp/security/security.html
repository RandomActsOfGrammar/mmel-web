<html>
<head>
<title>Sterling:  Sterling Example:  simple_imp:security</title>
<link href="../../../../style.css" rel="stylesheet" type="text/css">
<script src="../../../../actions.js"></script>
<link rel="icon" href="../../../images/favicon.png" type="image/x-icon">
</head>
<body class="sterling">
<div id="header-logo">
<a href="../../../index.html">
<img class="header-logo" src="../../../images/Sterling.png" alt="Sterling logo">
</a>
</div>
<h1>Sterling Example:  simple_imp:security</h1><div class="section">
<h3>File:  security.sos</h3>
<a class="fold-link" href="javascript:void" id="togglesecurity.sos" onclick="toggleFile('security.sos')">[Reduce File]</a><span> </span><a class="fold-link" href="security.sos">[Raw File]</a>
<pre class="code sterling" id="security.sos">
Module simple_imp:security

Builds on simple_imp:host

<span class="comment">/*New Statement and Translation*/</span>
c ::= ...
    | declareSec(string, seclevel, ty, e)


--------------------------------------------------- [Trans-DeclareSec]
|{c}- declareSec(X, S, Ty, E) ~~> declare(X, Ty, E)


seclevel ::= public | private
Translation seclevel :




<span class="comment">/*Typing and Evaluation for New Statement*/</span>

no_lookup G X <span class="comment">/*still can't overwrite existing decls*/</span>
typeOf G E Ty
------------------------------------------- [T-DeclareSec]
typeOK G declareSec(X, S, Ty, E) (X, Ty)::G


eval_e G E V
------------------------------------------ [E-DeclareSec]
eval_c G declareSec(X, S, Ty, E) (X, V)::G

</pre>
<h3>File:  analysis.sos</h3>
<a class="fold-link" href="javascript:void" id="toggleanalysis.sos" onclick="toggleFile('analysis.sos')">[Reduce File]</a><span> </span><a class="fold-link" href="analysis.sos">[Raw File]</a>
<pre class="code sterling" id="analysis.sos">
Module simple_imp:security

<span class="comment">/*Take two security levels and then get the higher one*/</span>
Judgment join : seclevel* seclevel seclevel

------------------------- [J-Publics]
join public public public


---------------------- [J-Private1]
join private S private


---------------------- [J-Private2]
join S private private




Judgment level : [(string, seclevel)] e* seclevel
Judgment rf_level : [(string, seclevel)] recFieldExprs* seclevel
Judgment secure : seclevel [(string, seclevel)] c* [(string, seclevel)]


<span class="comment">/*Expression Security Level*/</span>

--------------------- [S-Num]
level G num(I) public


level G E1 S1
level G E2 S2
join S1 S2 S
---------------------- [S-Plus]
level G plus(E1, E2) S


lookup G X S
----------------- [S-Name]
level G name(X) S


level G E1 S1
level G E2 S2
join S1 S2 S
------------------------- [S-Greater]
level G greater(E1, E2) S


level G E1 S1
level G E2 S2
join S1 S2 S
-------------------- [S-Eq]
level G eq(E1, E2) S


level G E1 S1
level G E2 S2
join S1 S2 S
--------------------- [S-And]
level G and(E1, E2) S


level G E1 S1
level G E2 S2
join S1 S2 S
-------------------- [S-Or]
level G or(E1, E2) S


------------------- [S-True]
level G true public


-------------------- [S-False]
level G false public


rf_level G Fields S
-------------------------- [S-RecBuild]
level G recBuild(Fields) S


level G Rec S
------------------------------------ [S-RecField]
level G recFieldAccess(Rec, Field) S


|{e}- E ~~> ET
level G ET S
-------------- [S-TransExpr]*
level G E S



<span class="comment">/*Record Field Security Level*/</span>

---------------------------------- [S-RFEnd]
rf_level G endRecFieldExprs public


level G E S1
rf_level G Rest S2
join S1 S2 S
----------------------------------------- [S-RFAdd]
rf_level G addRecFieldExprs(L, E, Rest) S


|{recFieldExprs}- RF ~~> RFT
rf_level G RFT S
---------------------------- [S-RFTrans]*
rf_level G RF S



<span class="comment">/*Statement Security*/</span>

------------------ [S-Noop]
secure PC G noop G


secure PC G C1 G1
secure PC G1 C2 G2
-------------------------- [S-Seq]
secure PC G seq(C1, C2) G2


level G E public
no_lookup G X <span class="comment">/*no previous assignment*/</span>
------------------------------------------------ [S-Declare]
secure public G declare(X, Ty, E) (X, public)::G


level G E S
no_lookup G X <span class="comment">/*no previous assignment*/</span>
------------------------------------------ [S-DeclareSecPrivate]
{secure PC G declareSec(X, private, Ty, E)
        (X, private)::G}


level G E public
no_lookup G X <span class="comment">/*no previous assignment*/</span>
--------------------------------------------- [S-DeclareSecPublic]
{secure public G declareSec(X, public, Ty, E)
        (X, public)::G}


<span class="comment">/*To assign to public var, need both PC and expr to be public*/</span>
level G E public
lookup G X public
------------------------------ [S-AssignPublic]
secure public G assign(X, E) G


<span class="comment">/*Can assign anything to a private var, anytime*/</span>
level G E S
lookup G X private
-------------------------- [S-AssignPrivate]
secure PC G assign(X, E) G


<span class="comment">/*Like assignment, since it is a version of it*/</span>
level G E public
lookup G R public
------------------------------------ [S-RecUpdatePublic]
secure public G recUpdate(R, F, E) G


level G E S
lookup G R private
-------------------------------- [S-RecUpdatePrivate]
secure PC G recUpdate(R, F, E) G


level G Cond S
join PC S NewPC
secure NewPC G Then GTh
secure NewPC G Else GEl
------------------------------------------ [S-IfThenElse]
secure PC G ifThenElse(Cond, Then, Else) G
<span class="comment">/*branches are new scopes, so we throw their security updates away*/</span>


level G Cond S
join PC S NewPC
secure NewPC G Body G1
------------------------------- [S-While]
secure PC G while(Cond, Body) G
<span class="comment">/*body is a new scope*/</span>


|{c}- C ~~> CT
secure PC G CT G1
----------------- [S-Trans]*
secure PC G C G1

</pre>
<div>
</body>
</html>
