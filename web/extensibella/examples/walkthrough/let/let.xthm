Module walkthrough:let.

Prove walkthrough:host:typeOf_unique.
%T-Let
 TyB: case TyB. apply IH to TyA1 TyB. apply IH to TyA2 TyB1. search.


Prove walkthrough:host:ty_lookup.
%T-Let
 apply IH to Ty1 L. apply IH to Ty2 _ with Ctx2 = (X, Ty1)::Ctx2.
   intros Lkp. Lkp: case Lkp.
     %Lkp-Here
      search.
     %Lkp-Later
      apply L to Lkp1. search.
 search.


Prove walkthrough:host:subst_type_preservation.
%S-Let-NEq
 Ty: case TTy. apply IH to Ty S2 RTy.
 Ty': apply ty_lookup to Ty1 _ with Ctx2 = (X, XTy)::(Y, Ty1)::Ctx.
   intros L. L: case L.
     %Lkp-Here
      search.
     %Lkp-Later
      L: case L1.
        %Lkp-Here
         search.
        %Lkp-Later
         search.
 apply IH to Ty' S3 RTy. search.
%S-Let-Eq
 Ty: case TTy. apply IH to Ty S1 RTy.
 apply ty_lookup to Ty1 _ with Ctx2 = (X, Ty1)::Ctx.
   intros L. L: case L.
     %Lkp-Here
      search.
     %Lkp-Later
      L: case L1.
        %Lkp-Here
         apply L to _.
        %Lkp-Later
         search.
 search.


Prove walkthrough:host:type_preservation.
%E-Let-Step
 Ty: case Ty. apply IH to Ty Ev1. search.
%E-Let-Subst
 Ty: case Ty. apply subst_type_preservation to Ty1 Ev2 Ty. search.


Prove_Constraint walkthrough:host:trans_type_same.
%Trans-Let
 case Hyp1. apply typeOf_unique to Hyp2 H1. search.


Prove_Ext_Ind walkthrough:host:typeOf.
%T-Let
 Acc: case Acc. unfold. apply drop_ext_ind_typeOf to Rel3.
 assert G |{tm}- let X T1 T2 ~~> app (abs X Ty1 T2) T1.
 IsN2: apply ext_ind_is_int_typeOf to Rel3.
 IsN3: apply ext_ind_is_int_typeOf to Rel4.
 IsN4: apply plus_integer_is_integer to _ _ Rel2.
 L: apply lt_plus_one to Rel1 _.
 PosN2: apply ext_ind_pos_typeOf to Rel3.
 PosN3: apply ext_ind_pos_typeOf to Rel4.
 assert <typeOf {T}> G T1 Ty1.
   Or: apply lt_left to Rel2 _ _. L': case Or.
     %N2 < N4
      L'': apply less_integer_transitive to L' L.
      A: apply Acc to _ L''. apply IH to Rel3 A. search.
     %N2 = N4
      A: apply Acc to _ L. apply IH to Rel3 A. search.
 assert <typeOf {T}> ((X, Ty1)::G) T2 Ty.
   Or: apply lt_right to Rel2 _ _ _. L': case Or.
     %N3 < N4
      L'': apply less_integer_transitive to L' L.
      A: apply Acc to _ L''. apply IH to Rel4 A. search.
     %N3 = N4
      A: apply Acc to _ L. apply IH to Rel4 A. search.
 exists app (abs X Ty1 T2) T1, Ty1. search.
